set windows-shell := ["pwsh.exe", "-NoLogo", "-Command"]
set shell := ["bash", "-c"]

make_dir := if os_family() == "windows" { "New-Item -ItemType Directory -Force" } else { "mkdir -p" }
env_var_prefix := if os_family() == "windows" { "$env:" } else { "" }
env_var_postfix := if os_family() == "windows" { ";" } else { "" }

default:
    @just -l

[doc('Build binary target in debug for native CPU')]
[group('dev')]
build-native target:
    cargo rustc --bin {{ target }} -- -C target-cpu=native

[doc('Build binary target in debug for native CPU')]
[group('dev')]
build-native-release target:
    cargo rustc --release --bin {{ target }} -- -C target-cpu=native

[doc('Build the project (default is debug)')]
[group('dev')]
build config="debug":
    echo "Building the project..."
    cargo build --workspace {{ if config == "release" { "--release" } else { "" } }}

[doc('Build and run tests (default is debug)')]
[group('dev')]
test config="debug":
    echo "Running tests..."
    cargo test --workspace {{ if config == "release" { "--release" } else { "" } }} -- --include-ignored

export LLVM_PROFILE_FILE := "./target/coverage/byte_knight-%p-%m.profraw"

[doc('Generate test coverage')]
[group('dev')]
coverage: (build "debug")
    echo "Running tests with coverage..."
    {{ make_dir }} target/coverage
    {{ env_var_prefix }}RUSTFLAGS="-Cinstrument-coverage"{{ env_var_postfix }} \
    cargo test --workspace -- --skip "perft"
    grcov target/coverage engine/target/coverage chess/target/coverage -s . \
        --binary-path ./target/debug/ --output-types lcov -o ./target/coverage/byte-knight.lcov \
        --branch --keep-only "src/*" --keep-only "engine/*" --keep-only "chess/*" \
        --ignore "src/bin/byte-knight/*" --ignore "chess/src/perft*"

[doc('Purge files generated by @coverage')]
[group('dev')]
purge-coverage:
    echo "Purging coverage data..."
    rm -rf *.profraw
    rm -rf target/coverage
    rm -rf chess/target
    rm -rf engine/target
    rm -rf chess/*.profraw
    rm -rf engine/*.profraw

[doc('Create HTML report from coverage data')]
[group('dev')]
coverage-report:
    echo "Generating HTML report..."
    genhtml \
        --branch \
        -o ./target/coverage/html \
        ./target/coverage/byte-knight.lcov

[doc('Run clippy')]
[group('dev')]
lint:
    cargo clippy --all --tests -- -D warnings

[doc('Run sarch benchmark - required before committing for OpenBench.')]
[group('chess')]
[group('performance')]
search-bench: (build-native-release "byte-knight")
    echo "Running search benchmark..."
    ./target/release/byte-knight bench

[doc('Run perft at a specified depth')]
[group('chess')]
perft depth:
    echo "Running perft..."
    cargo run --release --bin perft -- -d {{ depth }}

[doc('Run perft over the EPD test suite')]
[group('chess')]
perft-epd:
    echo "Running EPD perft test suite..."
    cargo run --release --bin perft -- --epd-file data/standard.epd

[doc('Run perft benchmark over the EPD test suite')]
[group('chess')]
[group('performance')]
perft-bench: (build-native-release "perft-bench")
    echo "Running perft benchmark..."
    target/release/perft-bench -e data/standard.epd

[doc('Generate magic numbers use for magic bitboards')]
[group('chess')]
magics:
    echo "Generating magics..."
    cargo run --release --bin generate_magics

[doc('Verify that generated Zobrist hashes are unique')]
[group('chess')]
verify-zobrist:
    echo "Verifying Zobrist hash..."
    cargo run --release --bin verify_zobrist

[doc('Generate release binaries for given target.')]
[group('dev')]
release target:
    echo "Building release binaries..."
    cargo rustc --release --bin byte-knight --target={{ target }}

[doc('Caches the release binary to bk-main for testing.')]
[group('dev')]
cache-main: (build-native-release "byte-knight")
    echo "Caching binary for testing..."
    cp target/release/byte-knight ./bk-main

[doc('Run the engine against itself. Requires @cache-main to be run first and fastchess to be installed.')]
[group('chess')]
[group('dev')]
compare-to-main: (build-native-release "byte-knight")
    echo "Comparing byte-knight to bk-main"
    fastchess -engine cmd="./target/release/byte-knight" name="dev" -engine cmd="./bk-main" name="bk-main" -openings file="./data/Pohl.epd" format=epd order=random -each tc=10+0.1 -rounds 200 -repeat -concurrency 8 -sprt elo0=0 elo1=5 alpha=0.05 beta=0.1 model=normalized -output format=cutechess

[doc('Format all Rust code')]
[group('dev')]
format:
    cargo fmt --all

[doc('Run the HCE tuner on the given input book')]
[group('dev')]
hce-tune book epochs: (build-native-release "hce-tuner")
    echo "Running HCE tuner..."
    ./target/release/hce-tuner tune -i {{ book }} -e {{ epochs }} -p engine-values
